//
// Copyright (C) 2021 - 2023, SanteSuite Inc. and the SanteSuite Contributors (See NOTICE.md for full copyright notices)
// Copyright (C) 2019 - 2021, Fyfe Software Inc. and the SanteSuite Contributors
// Portions Copyright (C) 2015-2018 Mohawk College of Applied Arts and Technology
// 
// Licensed under the Apache License, Version 2.0 (the "License"); you 
// may not use this file except in compliance with the License. You may 
// obtain a copy of the License at 
// 
// http://www.apache.org/licenses/LICENSE-2.0 
// 
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
// WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the 
// License for the specific language governing permissions and limitations under 
// the License.
//
define library "SanteDB Core CDSS Fact Library"
	having id <org.santedb.cdss.core>
	having uuid {401997f5-731d-4e46-8c4c-3c5bd7b2b58b}
	having status active
	with metadata
		author SanteSuite Inc. and the SanteSuite Contributors
		version 3.0-alpha1
		doc This library contains core logic to extract common facts related to objects in SanteDB. It defines
		doc no rules, protocols, or models, rather is intended to provide common facts about data to allow
		doc users to more easily derive their own rules
	end metadata
	as
	
	define logic "SanteDB Core CDSS Context Facts" 
	    having id <org.santedb.cdss.core.identifiedData>
	    having status active 
	    having context Entity
	    with metadata 
	        version 3.0-alpha
	        doc Core CDSS Facts for all objects
	   end metadata
	   as
	    
	    define fact "CDS Running Non-Interactive" having type bool with metadata
	        doc Indicates that the CDSS engine is running in a non-interactive mode such as during a re-calculation
	    end metadata as 
	        csharp($$Boolean.Parse(context["isBackground"]?.ToString() ?? "false")$$)
	   end fact 
	   
	   define fact "CDS Running in Test Mode" having type bool with metadata 
	    doc Indicates that the CDSS engine is running as part of a test mode rather than in a production environment
	   end metadata as
	    csharp($$Boolean.Parse(context["isTesting"]?.ToString() ?? "false")$$)
	   end fact 
	end logic
	define logic "SanteDB Core CDSS Patient Facts"
		having id <org.santedb.cdss.core.patient>
		having status active
		having context Patient		with metadata
			version 3.0-alpha
			doc Core CDSS facts for Patients
		end metadata
		as
		define fact "Patient Is Deceased"
			having type bool
		as
			hdsi($$deceasedDate=!null$$)
		end fact
		define fact "Patient is not Deceased"
			having type bool
		as
			none(
				"Patient Is Deceased"
			)

		end fact
		define fact "Patient Gender"
			having type string
		as
			hdsi($$genderConcept.mnemonic$$)
		end fact
		define fact "Patient is Female"
			having type bool
		as
			hdsi($$genderConcept=094941e9-a3db-48b5-862c-bc289bd7f86c$$)
		end fact
		define fact "Patient is Male"
			having type bool
		as
			hdsi($$genderConcept=f4e3a6bb-612e-46b2-9f77-ff844d971198$$)
		end fact
		define fact "Patient Age In Years"
			having type real
			with metadata
				doc Expresses the patient's age in years (including partial years expressed as a decimal)
			end metadata
		as
			csharp($$DateTime.Now.Subtract(context.Target.DateOfBirth.Value.Date).TotalDays / 365.25f$$)
		end fact
		define fact "Patient Date of Birth"
			having type date
		as
			hdsi($$dateOfBirth$$)
		end fact
		define fact "Patient is HIV Positive"
		    having type bool 
		as 
		    any(
		        // Finding of HIV Status = HIV Positive
		        hdsi($$participation[RecordTarget].source.typeConcept=8a07a21f-50ff-4631-8e51-c54690fb1127&
		            participation[RecordTarget].source@CodedObservation.value=f36f692d-798b-4883-8d83-3ecf6c1571d0&
		            participation[RecordTarget].source.isNegated=false$$),
		        // Active problem/concern entry
		        hdsi($$participation[RecordTarget].source.typeConcept=260ffe90-7882-4b38-a7af-d2110e91e752&
		            participation[RecordTarget].source.relationship[HasSubject].target.value=f36f692d-798b-4883-8d83-3ecf6c1571d0&
		            participation[RecordTarget].source.relationship[HasSubject].target.typeConcept=8a07a21f-50ff-4631-8e51-c54690fb1127&
		            participation[RecordTarget].statusConcept=C8064CBD-FA06-4530-B430-1A52F1530C27$$)
		  )
	    end fact 
	    define fact "Patient is Confirmed HIV Negative" 
	        having type bool 
	   as 
	        hdsi($$participation[RecordTarget].source.typeConcept=8a07a21f-50ff-4631-8e51-c54690fb1127&
	            participation[RecordTarget].source@CodedObservation.value=dd3a4ad8-ec64-44a5-a3aa-8dad28a49d25&
	            participation[RecordTarget].source.isNegated=false$$)
	   end fact
	end logic
	define logic "SanteDB Core CDSS for Acts"
		having id <org.santedb.cdss.core.act>
		having status active
		having context Act		with metadata
			version 3.0-alpha
			doc Core CDSS facts for all Acts
		end metadata
		as
		define fact "Record Target for Act"
		as
			hdsi($$participation[RecordTarget].player@Entity$$)
		end fact
		define fact "Record Target Gender"
			having type string
		as
			hdsi($$participation[RecordTarget].player@Person.genderConcept.mnemonic$$)
		end fact
		define fact "Record Target Date of Birth"
			having type date
		as
			hdsi($$participation[RecordTarget].player@Person.dateOfBirth$$)
		end fact
		define fact "Record Target Age in Years"
			having type real
		as
			csharp($$(float)DateTime.Now.Subtract(((DateTime?)context["Record Target Date Of Birth"]).Value).TotalDays / 365.25f$$)
		end fact
	end logic
end library
