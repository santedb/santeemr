//
// Copyright (C) 2021 - 2023, SanteSuite Inc. and the SanteSuite Contributors (See NOTICE.md for full copyright notices)
// Copyright (C) 2019 - 2021, Fyfe Software Inc. and the SanteSuite Contributors
// Portions Copyright (C) 2015-2018 Mohawk College of Applied Arts and Technology
// 
// Licensed under the Apache License, Version 2.0 (the "License"); you 
// may not use this file except in compliance with the License. You may 
// obtain a copy of the License at 
// 
// http://www.apache.org/licenses/LICENSE-2.0 
// 
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
// WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the 
// License for the specific language governing permissions and limitations under 
// the License.
//
include <org.santedb.cdss.core>
define library "SanteDB Core CDSS Fact Library for Children"
	having id <org.santedb.cdss.core.childcare>
	having uuid {876fffad-fc37-4666-b949-4a96ae2a4fb9}
	having status active
	with metadata
		author SanteSuite Inc. and the SanteSuite Contributors
		version 3.0-alpha
		doc Contains core CDSS facts which are useful for protocols and rules involving children
	end metadata
	as
	define logic "Core Childhood Facts for Patient"
		having id <org.santedb.cdss.core.childcare.patient>
		having status active
		having context Patient		
		with metadata
			version 3.0-alpha
		end metadata
		as
		define fact "Patient is Infant"
			having type bool
			with metadata
				doc Identifies a patient as an infant (defined as a patient whose age is under 18 months old)
			end metadata
		as
			hdsi($$dateOfBirth=:(age)<P18M$$)
		end fact
		define fact "Patient Is a Child"
			having type bool
			with metadata
				doc Identifies if a patient is a "child" (defined as a patient being under 12 years old - includes infants)
			end metadata
		as
			hdsi($$dateOfBirth=:(age)<P12Y$$)
		end fact
		define fact "Patient is Adolescent"
			having type bool
			with metadata
				doc Identifies whether a patient is an adolescent (defined as a patient being between 12 and 19 years old)
			end metadata
		as
			all(
				none(
					"Patient Is a Child"
				),
				hdsi($$dateOfBirth=:(age)<P20Y$$)
			)

		end fact
		define fact "Patient Age in Days"
			having type int
			with metadata
				doc Provides the patient's current age expressed as a whole number of days
			end metadata
		as
			csharp($$(long)DateTime.Now.Subtract(context.Target.DateOfBirth.Value).TotalDays$$)
		end fact
		define fact "Patient Age in Months"
			having type int
			with metadata
				doc Provides the patient's current age expressed as a whole number of months
			end metadata
		as
			csharp($$(DateTime.Now.Year * 12 + DateTime.Now.Month) - (context.Target.DateOfBirth.Value.Year * 12 + context.Target.DateOfBirth.Value.Month)$$)
		end fact
		define fact "Patient Age in Weeks"
			having type int
			with metadata
				doc Provides the patient's current age expressed as a whole number of weeks
			end metadata
		as
			csharp($$(int)(DateTime.Now.Subtract(context.Target.DateOfBirth.Value).TotalDays / 7)$$)
		end fact
	end logic
	define logic "Core Childhood Facts for Acts"
		having id <org.santedb.cdss.core.childcare.act>
		having status active
		having context Act		with metadata
			version 3.0-alpha
		end metadata
		as
		define fact "Record Target is Infant"
			having type bool
			with metadata
				doc Identifies whether the patient of the act as an infant (defined as a patient whose age is under 18 months old)
			end metadata
		as
			hdsi($$participationp[RecordTarget].player@Person.dateOfBirth=:(age)<P18M$$)
		end fact
		define fact "Record Target Is a Child"
			having type bool
			with metadata
				doc Identifies whether the patient of the act is a "child" (defined as a patient being under 12 years old - includes infants)
			end metadata
		as
			hdsi($$participationp[RecordTarget].player@Person.dateOfBirth=:(age)<P12Y$$)
		end fact
		define fact "Record Target is Adolescent"
			having type bool
			with metadata
				doc Identifies whether the patient of the act is an adolescent (defined as a patient being between 12 and 19 years old)
			end metadata
		as
			all(
				none(
					"Record Target Is a Child"
				)
,
				hdsi($$participationp[RecordTarget].player@Person.dateOfBirth=:(age)<P20Y$$)
			)

		end fact
		define fact "Record Target Age in Days"
			having type int
			with metadata
				doc Provides the patient of this act's current age expressed as a whole number of days
			end metadata
		as
			csharp($$(long)DateTime.Now.Subtract((DateTime)context["Record Target Date of Birth"]).TotalDays$$)
		end fact
		define fact "Record Target Age in Months"
			having type int
			with metadata
				doc Provides the patient of this act's current age expressed as a whole number of months
			end metadata
		as
			csharp($$(DateTime.Now.Year * 12 + DateTime.Now.Month) - (((DateTime)context["Record Target Date of Birth"].Year) * 12 + ((DateTime)context["Record Target Date of Birth"]).Month)$$)
		end fact
		define fact "Record Target Age in Weeks"
			having type int
			with metadata
				doc Provides the record target's current age expressed as a whole number of weeks
			end metadata
		as
			csharp($$(int)(DateTime.Now.Subtract((DateTime)context["Record Target Date of Birth"]).TotalDays / 7)$$)
		end fact
	end logic
end library
